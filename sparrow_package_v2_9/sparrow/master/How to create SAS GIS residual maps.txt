How to Create a SAS GIS Coverage That Automatically Merges Points with a Polygon Coverage

There are two stages to production. The first stage imports the ARC polygon coverage
(serving as the backgroud for the map), converts it to latlon coordinates, imports a
dummy point coverage (representing the station locations), converts it to latlon 
coordinates, defines a theme for the point coverage, merges the two coverages, and 
defines a map for the merged coverages. The merged coverages are saved in a permanent
directory. The second stage is run when the point data are updated. In this stage, the
point data are imported and converted to latlon coordinates. When the merged coverage 
is plotted, it shows the merger with the updated point coverage.

Stage I - A quasi-batch method for creating the residuals map.

  1. To import the ARC coverage serving as the background (states2mprj), run the following code:

       /* This code creates the map and spatial coverage */

       libname dir_gis "d:\greg\sparrow\sparrow_gis\test" ;
       filename arcin "d:\greg\sparrow\sparrow_gis\states2mprjp.e00" recfm = v lrecl = 80 ;

       %let IMP_TYPE = arc ;
       %let MAPLIB = dir_gis ;
       %let MAPCAT = states2mprj ;
       %let MAPNAME = states2mprj ;
       %let CATHOW = create ;
       %let SPALIB = dir_gis ;
       %let SPANAME = states2mprj ;
       %let SPAHOW = create ; 

       DM 'AF C=SASHELP.GISIMP.batch.SCL';

       proc gis ;
         catalog dir_gis.states2mprj ;
         spatial update states2mprj / latlon degrees ;
         map update states2mprj / latlon degrees ;
         layer update dir_gis.states2mprj.states2m / default = (area = (color = CXBDB8B3)) ;
       run ;
       quit ;

     To allow you to select features on the map using a where clause, the background coverage (STATES2M) must
     have the same reference variable as is used by the point data to link to point attributes. This variable
     must be stored in the chain file of the background coverage and a composite must be created so that this
     varible is recognized as relevant to selection in the background layer. Add the variable 'id' to the chain
     data set dir_gis.states2mprjc and add the composite to the spatial entry dir_gis.states2mprj.states2mprj.

       data dir_gis.resids_backc ; set dir_gis.resids_backc ;
         id = 0 ;
       run ;

       proc gis ;
         spatial update dir_gis.resids_back.resids_back ;
         composite create id / var=id ;
       run ;
       quit ;


  2. Create a dummy set of points corresponding to the station locations and dummy values for
     the standardized residuals. To do this, first create a SAS file that has at least the
     following variables (it may have other attributes for each station):

           staid - A unique identifier for each station point.
             lon - Decimal longitude, with negative values for western hemisphere.
             lat - Decimal latitude.
       map_resid - Value of the standardized residual (residual divided by its standard error). If
                   a standardized residual is missing, use the usual missing indicator ".". For the
                   dummy dataset, make at least one value a missing value.

     Create the library dir_rslt and save the SAS file as dir_rslt.mapresids.

  3. Import this file into a SAS/GIS coverage saved in the Work directory by running the following
     SAS batch import program:

       data resids (keep = staid lat lon rename = (lat = y lon = x)) ; set dir_rslt.resids ;
       run ;
  
       %let IMP_TYPE = genpoint ;
       %let INFILE = mapresids ;
       %let NIDVARS = 0 ;
       %let MAPLIB = dir_gis ;
       %let MAPCAT = resids ;
       %let MAPNAME = resids ;
       %let CATHOW = create ;
       %let SPALIB = dir_gis ;
       %let SPANAME = resids ;
       %let SPAHOW = create ; 

       DM 'AF C=SASHELP.GISIMP.BATCH.SCL';

  4. Run proc gis twice to set the layers for mapres. Note that this approach does not use the SPECIFIED option 
     that is only available using the Windows version of SAS/GIS. You will have to go back into the mapres.mapres
     map and edit the layer (see iterm 8 in Stage I below) so that you specify the break points at -2 and 2. 
     You should not have to redefine the color, font or characters.

       proc gis ;
         layer update dir_gis.resids.mapresids / default = (point = (size = 4 font = marker character = 'W')) ;
       run ;
       quit ;

       proc gis ;
         spatial update dir_gis.resids.resids / latlon degrees ;
         map update dir_gis.resids.resids / latlon degrees ;
         layer update dir_gis.resids.mapresids / thematic 
           theme = 
             (create
              themevar = map_resid
              dataset = dir_rslt.resids
              datavar = staid
              composite = staid
              link = mapresids
              range = levels                                               
              nlevels = 4 
              point = (
                (level = 1 size = 8 color = CXE40000 font = marker character = 'D')
                (level = 2 size = 6 color = CX409C6E font = marker character = 'D')
                (level = 3 size = 6 color = CX409C6E font = marker character = 'C')
                (level = 4 size = 8 color = CX4580CF font = marker character = 'C')
                )
              ) ;
       run ;
       quit ;

  5. Run the following SAS program to merge the polygon and point coverages to create a
     new coverage/map called dir.plotres that is stored in the dir directory.

       proc gis ;
         spatial create dir_gis.residmap.residmap / merge = (dir_gis.states2mprj.states2mprj,dir_gis.resids.resids) ;
         coverage create dir_gis.residmap.residmap / where = '1' ;
         map create dir_gis.residmap.residmap / latlon degrees coverage = dir_gis.residmap.residmap
         layers = (dir_gis.states2mprj.states2m,dir_gis.resids.mapresids) details layerson = (_all_) ;
       run ;
       quit ; 

  6. Run the following SAS program to set the label:

       data dir_gis.residmap_label ;
         row = 0 ;
         x = -345 ;
         y = 46 ;
         ftype = 'L' ;
         layer = '' ;
         spatial = '' ;
         ltype = 'T' ;
         onscale = 0 ;
         offscale = 0 ;
         text = 'Studentized Residuals Map' ;
         style = 'Arial-21.7pt-Roman-Normal' ;
         color = 'CX336CD7' ;
         flags = 133634 ;
       run ;

       proc gis ;
         map update dir_gis.residmap.residmap / label = dir_gis.residmap_label ;
       run ;
       quit ;

  7. Load the resids map into SAS/GIS and edit. In SAS/GIS, click on File, Open Map and 
     select the dir_gis directory, the resids catalog and then the resids map.

     Edit the map by doing the following:

       Create a Theme for the Mapresids layer:

         Right click on the Mapresids layer box and select Edit.
         Click on the Thematic box.
         Click on the Theme Range box - this opens the GIS Thematic Layer Ranges definition window.
         Click on the Specified box.
         Click on Add Break and enter -1.5.
         Click on Add Break and enter 1.5
         Click on Remove Break and select all values except -1.5, 0, and 1.5.
         Click OK - this returns you to the GIS Layer window where you can now edit the symbols.

       Save the map by clicking on File, Save, All.

     Load the Residmap map into SAS/GIS and edit. In SAS/GIS, click on File, Open Map and 
     select the dir_gis directory, the residmap catalog and then the residmap map icon (a globe). The map should 
     display with both the States2m and Mapresids layers showing and with the details showing
     for the States2m layer. If this is not the case, then be sure both layer boxes are checked
     and right click on the States2m layer box and select Show Details.

       Set the Map Display Projection:

         Click on Tools, Map Properties, Map Options to open the Map Options window.
         Click on the Projections box to open the Projection Options window.
         Set the Display Projection System to Alber's Conical - you may have to reduce the Units Multiplier
           to 10000.
         Click the OK box to confirm selection and answer Yes to the Save Changes request.

       Set the Map Background Color:

         Click on Tools, Map Properties, Colors... to open the Map Styles and Colors window.
         Set the Background color to CXE1E9DA.
         Click on OK and Close and respond Yes to the Save Changes request.

       Create a Legend for the Map:

         Click on View, Legend, New to open the Lengend Options window.
         Click on MAPRESIDS, deselect Frame, be sure Show Missing Values is set.
         Set the Text Attributes to be Font: Ariel, 12 point, Color: CX336CD7 (blue).
         Enter the footnote text: (+) under-predict, (-) over-predict.
         Click OK and place the legend on the map. If you need to edit the location or contents then
           right click over the legned and select Edit.

       If you need to edit the title location, do the following:

         Right click on the existing title and select Move...
         Move the title to the desired location.

       Create an Action that allows you to click on a point and have the point displayed in FSView table.

         Click on Tools, Action, Define

       Make the Mapresids layer selectable by right-clicking on the Mapresids button above the map and
       selecting Make Layer Selectable

       Save the map by clicking on File, Save, All.

  8. Exit SAS

  This completes Stage 1.


Stage II. 

  1. Create a new mapresids file (in the work directory). This file may contain new points as well as new values.

  2. To update the GIS coverage with new residual values, run the following:

       libname dir_gis clear ;
       libname dir_gis "d:\greg\sparrow\sparrow_gis\test" ;

       %let IMP_TYPE = genpoint ;
       %let INFILE = mapresids ;
       %let NIDVARS = 0 ;
       %let MAPLIB = dir_gis ;
       %let MAPCAT = mapres ;
       %let MAPNAME = mapres ;
       %let CATHOW = update ;
       %let SPALIB = dir_gis ;
       %let SPANAME = mapres ;
       %let SPAHOW = update ; 

       DM 'AF C=SASHELP.GISIMP.BATCH.SCL';

  3. The map Plotres in the catalog dir_gis.plotres will have the plot of the standardized residuals.
     The map can be opened by starting SAS/GIS and opening the map dir.plotres.plotres. 

  4. The map can saved to graphics output by doing the following:

       In SAS/GIS, click on File, Print Options to open the Print Options window.
       Select Windows Metafile (WMF) under Device, Export
       Check the box Suppress Line Width Scaling
       Click OK
       Click on File, Save As, Graphics Output, and place the graph in the dir.plotres catalog.

Additional Notes:

  To view the contents of a layer or map, do the following:

    proc gis ;
      layer contents dir_gis.mapres.mapresids ;
    run ;
    quit ;

  or,

    proc gis ;
      map contents dir_gis.plotres.plotres ;
    run ;
    quit ;
  
  Note that the contents will display in the OUTPUT window only if the run command is entered BEFORE 
  the quit command.